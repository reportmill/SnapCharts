
plugins {
    id 'application'
    id 'java-library'
    id 'maven-publish'
}

group 'com.reportmill'
version '1.0.0'

sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['src']

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {

    mavenCentral()

    // Jitpack.io builds Github packages
    maven { url 'https://jitpack.io' }
}

// SnapKit version (might want 'master-SNAPSHOT', '2021.06.29', '<tag_hash>', etc.)
project.ext.snapkitVersion = '2021.09.19'

dependencies {

    // If Local SnapKit project, use it
    if (findProject(':SnapKit') != null) {
        println 'Using local SnapKit project'
        compile project(':SnapKit')
    }

    // Otherwise use GitHub SnapKit repo build
    else {
        println 'Using GitHub/Jitpack SnapKit package'
        compile "com.github.reportmill:SnapKit:$snapkitVersion"
    }
}

mainClassName = 'snapcharts.app.App'

publishing {

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/reportmill/SnapCharts"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
                println "GitHubPackages Publish Artifact:\n\tusername=$username\n\ttoken=$password"
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            artifactId 'snapcharts'
            from(components.java)
        }
    }
}
